buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

plugins {
    id 'org.openapi.generator' version '6.6.0'
    id 'maven-publish'
    id 'java-gradle-plugin'
    id 'net.nemerosa.versioning' version '2.8.2'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly 'jakarta.platform:jakarta.jakartaee-api:10.0.0'
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: '4.12.0'
    implementation group: 'io.swagger.codegen.v3', name: 'swagger-generator', version: '3.0.52'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations-jakarta', version: '2.2.20'
    implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2', version: '2.2.20'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '3.2.3'
    implementation("org.openapitools:jackson-databind-nullable:0.2.6")
}

openApiValidate {
    inputSpec = "${rootDir}/src/main/resources/openapi.yaml"
}

openApiGenerate {
    generatorName = 'jaxrs-spec'
    inputSpec = "$rootDir/src/main/resources/openapi.yaml"
    configFile = "$rootDir/src/main/resources/config.yaml"
    outputDir = "$rootDir/"
    configOptions = [
            "useJakartaEe": "true",
            "delegatePattern": "true"
    ]
}

java {
    withSourcesJar()
}

sourceSets {
    main {
        java {
            srcDir "$rootDir/src/gen/java"
        }
    }
}

clean {
   delete "$rootDir/src/gen/"
}

tasks.register('fixJavaxJakarta') {
    doLast {
        ant.replaceregexp(match: 'import javax', replace: 'import jakarta', flags: 'g', byline: true) {
            fileset(dir: "$rootDir/src/gen/java/org/lankheet/domiot/api", includes: "*.java")
        }
    }
}

tasks.fixJavaxJakarta.dependsOn('openApiGenerate')
tasks.compileJava.dependsOn('fixJavaxJakarta')
tasks.publishToMavenLocal.dependsOn('compileJava')

publishing {
    publications {
        maven(MavenPublication) {
            groupId findProperty('group')
            artifactId findProperty('name')
            version findProperty('version') + '-' + findProperty('classifier')

            from components.java
        }
    }
}
