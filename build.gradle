import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

buildscript{
   repositories{
      mavenLocal()
      maven{url "https://repo1.maven.org/maven2"}
   }
}

plugins{
   id 'org.openapi.generator' version '6.2.1'
   id 'maven-publish'
   id 'java-gradle-plugin'
   id 'net.nemerosa.versioning' version '2.8.2'
}

version '1.0.1-SNAPSHOT'

openApiGenerate{
   generatorName = 'jaxrs-spec'
   inputSpec = "$rootDir/src/main/resources/openapi.yaml".toString()
   configFile = "$rootDir/src/main/resources/config.yaml".toString()
   outputDir = "$buildDir/generated/java".toString()
}

task openaApiGenerateTs(type: GenerateTask){
   generatorName = "typescript-angular"
   inputSpec = "$rootDir/src/main/resources/openapi.yaml".toString()
   outputDir = "$buildDir/generated/ts".toString()
   apiPackage = "com.lankheet.lds.api"

   configOptions = [
      ngVersion : "14.2.11",
      npmName   : "${project.name}-client",
      npmVersion: "${project.version}",
      modelTests: "false",
      //      npmRepository: project.NEXUS_UPLOAD_URL
   ]
}

task openApiGenerateSql(type: GenerateTask){
   generatorName = "mysql-schema"
   inputSpec = "$rootDir/src/main/resources/openapi.yaml".toString()
   outputDir = "$buildDir/generated/mysql".toString()
}

task copyGenerated(type: Copy){
   from "${rootDir}/build/generated/java/src/gen/java"
   into "${rootDir}/src/main/java"
}

copyGenerated.dependsOn('openApiGenerate')

task packageNpmLib{
   doLast{
      exec{
         workingDir "${buildDir}/generated/ts"
         executable 'npx'
         args 'ng', 'build', "${project.name}-client"
      }
      println "Executed!"
   }
}

packageNpmLib.dependsOn('openaApiGenerateTs')

compileJava.dependsOn('openApiGenerate')

sourceSets{
   main{
      java{
         srcDir "$buildDir/generated/src/main/java"
      }
   }
}

openApiValidate{
   inputSpec = "${rootDir}/src/main/resources/openapi.yaml".toString()
}

dependencies{
   compileOnly 'jakarta.platform:jakarta.jakartaee-api:9.1.0'
   implementation group: 'io.swagger.codegen.v3', name: 'swagger-generator', version: '3.0.36'
   implementation group: 'io.swagger.core.v3', name: 'swagger-annotations-jakarta', version: '2.2.6'
   implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2', version: '2.2.6'
}

publishing{
   publications{
      maven(MavenPublication){
         groupId 'com.lankheet.lds'
         artifactId 'iot-api'
         version version

         from components.java
      }
   }
}

